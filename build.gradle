buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

apply plugin: 'com.android.library'

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        minSdkVersion 3
        targetSdkVersion 23
    }

    lintOptions {
        xmlReport false
        warningsAsErrors true
        quiet false
        showAll true
        // asserts are not reliable
        // default locale is not defined in String.format calls
        // NTLM Auth does use DES/ECB/NoPadding which should not be used elsewhere
        // Default implementation of SecureRandom should be fixed by PRNGFixes.apply(), see http://android-developers.blogspot.cz/2013/08/some-securerandom-thoughts.html
        disable 'Assert', 'DefaultLocale', 'GetInstance', 'TrulyRandom'
    }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

apply from: 'maven_push.gradle'
